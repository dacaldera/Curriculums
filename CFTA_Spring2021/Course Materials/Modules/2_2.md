# Module 2.2

## The For Loop

* `for(){}` loops are used to repeat a command or code block a given number of times. 
* the for loop requires three components: declaration of the iterator, the condition, and the incrementation of the iterator
```
for(int i=0; i < 10; i++){
    //the code to be reapeated goes in here between the brackets
}
```
* Declaration of the iterator happens first like this `int i=0`. the variable name `i` is commonly used, but really the variable could be named anything. 
* The condition portion of the for loop determines how long the loop will run. As long as the condition is true, the loop will continue to run. The condition can be something simple like `i < 10` or it can even be something more complex like `i*12 < width/2` etc. As soon as the condition is not true, the loop stops and exits, and continues with the rest of the sketch.
* The incrementation portion of the loops is executed once after iteration, thereby changing the value of `i` by the specified amount. The commonly used `i++` adds 1 to the value of `i` after every iteration
* The three components are separated by a semi-color `;`. Notice how there are only two semi-colons in the for loop: `for(int i = 0 ; i<10 ; i++){}`
* Putting the three components together creates a for loop that iterates the given number of times and exits without any errors. 
* the iterator `i` can then be used within the brackets of the for loop in any way desired, such as explained next.

## Uses of for loops
* for loops can be used to draw a shape or a line in a repeated fashion on the canvas, such as in a row of ellipses. This can be achieved by using the iterator `i` in place of the x or y (or both) component of a position. for example:
```
for(int i=0; i<10; i++){
  rect( i , 100, 10, 10); //the i variable is increasing from 0 to 10. The rect is drawn with the x position at each of those i values. 
}
```
* for loops can also be used to alter the color via the fill() or stroke() command. 
* for loops can be used to println() a range of numbers to the console box at the bottom of the sketch window. println(i) prints the value of i as it increases.
* there are countless other uses of for loops.



## Assignment: 
* Read this: [https://happycoding.io/tutorials/processing/for-loops#homework](https://happycoding.io/tutorials/processing/for-loops#homework) and complete the homework assignemnts at the bottom of the page. Do what you can, and aim for all of it. 

* Also, choose a video from here and watch it! [http://lillian.com/films/](http://lillian.com/films/);


## Basic for loop
```
void setup(){
  size(600,600);
}

void draw(){
  background(255);
  // draws 10 circles end to end
  for(int i=0;i<10;i++){
   ellipse(i*10,100, 10,10); 
  }
}
```

## Transformation Matrix
```
void setup(){
  size(600,600);
  rectMode(CENTER);
}

void draw(){
  background(255);
  
  pushMatrix();  //pushMatrix and popMatrix encapsulate what ever is between them to keep them separate
  translate(width/2, height/2);  //move the origin 0,0 to the center of the canvas
  rotate(radians(frameCount)); //the built-in frameCount variable is constantly increasing
  rect(0, 0, 50, 50); // draw it at 0,0 because the matrix has been transformed
  popMatrix();
  
}
```

## Nested and Standard For Loop Examples
```
PImage cat;
PImage grass;



void setup() {
  size(600, 600);
  cat = loadImage("orange_boy.png");
  grass = loadImage("grass.png");
}

void draw() {
  background(255);

  // nested for loop
  for (int j=0; j<height; j+=grass.height) {
    for (int i = 0; i<width; i+=grass.width) {
      image(grass, i, j);
    }
  }

  // standard for loop
  for (int i = 0; i < 5; i++) {
    stroke(0);
    image(cat, mouseX+(i*cat.width), mouseY);
  }
}
```
