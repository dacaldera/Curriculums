# Module 1.5

## Quick Review
* the second(), minute(), hour(), day(), month(), and year() functions return parts of the time. They are written in singular form, and dont forget to use the prenthesis. 
* the map() function takes one value range and then maps it over to a new value range. 
```
variable2 = map(variable1, min1, max1, min2, max2);
```

## Color Modes
* The colorMode() function is used to specify which color style to use. The two styles are *RGB* and *HSB*
* RGB stands for RED, GREEN, and BLUE. This is the default color mode. Each value is in a range between 0 and 255.  [RGB-Style Color Sliders](https://codepen.io/leemark/pen/lpEHr)
* The range for each value can be changed using the colorMode() function.
* For example, colorMode(RGB, 500,500,500, 500) changes the range (which was previously 255) to 500.
* Another example, colorMode(RGB, 1.0, 1.0, 1.0) changes the range to a float value between 0 and 1.
* Another style is HSB which stands for HUE, SATURATION, and BRIGHTNESS. [HSB-Style Color Wheel](https://codepen.io/HunorMarton/full/eWvewo)
* The HSB style is commonily used with the range of values of 360, 100, and 100 for H, S, and B respectively. This color mode can be set like this: `colorMode(HSB, 360, 100, 100)`
* Setting the HUE range to 360 allows you to easily cycle through the colors of the colorwheel using an incremental counter.
* It is possible to switch between color modes repeatedly within your sketch. When doing so, the range values will remain unchanged between color modes (If you have (colorMode(HSB, 360, 100, 100) and you change over to colorMode(RGB) without specifying the range values, then RGB will incorectly have the previously set range values of 360, 100, 100 applied). Therefore always set the color values when switching between modes.


## Alpha (Opacity)
* The fourth argument in a fill() function is the Alpha value or Opacity. 
* You might have heard or seen the term "RGB" before but have you seen or heard "RGBA"? The "A" stands for Alpha
* Every color value can have an opcatiy or alpha value set between 0 and 255 with 0 being completely transparent or invisible and 255 being completely opaque.
* When the Alpha value is omittied in a fill() or stroke() function, the default value of 255 is applied. 
* One interesting effect is setting the background color to include an alpha transparency value between 0 and 255 such as 50. Any graphic element that is drawn to the screen will slowly fade over time. 
* alpha can also be used for layering effects using shapes and colors. 

## Blend Modes
* The blendMode() command sets the type of blending that occurs when two colors are overlaid on top of each other.
* the blendMode() command has many options. The defualt is "BLEND".

`blendMode(ADD)`
`blendMode(SUBTRACT)`
`blendMode(DIFFERENCE)`
`blendMode(EXCLUSION)`
`blendMode(MULTIPLY)`
`blendMode(SCREEN)`
`blendMode(LIGHTEST)`
`blendMode(DARKEST)`
* Try using this code block to test out the different blend modes
```
void setup() {
  size(500, 500);
  background(150);
  noStroke();
}

void draw() {
  //Try changing the contents of blendMode to a different style
  blendMode(DIFFERENCE); 
  
  //fills the ellipse with a random color
  fill(random(255), random(255), random(255), random(255));
  
  //draw an ellipse at a random position within the canvas
  ellipse(random(width), random(height), 100,100);
}
```

## Using the Color datatype
* A color can be a datatype like int, float, and String.
`color my_color = color(255,0,0);`
* In the example above, we declare `my_color` using the color datatype. Then we assign it a color using the color() function.
* now we can use `my_color` in the fill and stroke commands like this: `fill(my_color)`
* This makes it easier to manage multiple colors instead of creating variables for each of the reds, greens, and blues. 





## More IF Statement Practice
* This excersize introduces a well rounded algorithym called "Random Walker" that produces an interesting visual output.

```
float x;
float y;

void setup() {
  size(200, 100);
  
  //start in middle of screen
  x = width/2;
  y = height/2;

  //gray background
  background(200);
  
  //make the simulation faster
  frameRate(1000);
}

void draw() {
  
  stroke(0);
  
  //randomly move
  x += random(-1, 1);
  y += random(-1, 1);
  
  //prevent going off left or right
  if(x < 0){
    x = width;
  }
  if(x > width){
    x = 0;
  }

  //prevent going off top or bottom
  if(y < 0){
    y = height;
  }
  if(y > height){
    y = 0;
  }
  
  //draw the point
  point(x, y);
}
```
* Consider how the code could be changed to incorporate color to the walker
* What other elements could be altered for interesting effects?
